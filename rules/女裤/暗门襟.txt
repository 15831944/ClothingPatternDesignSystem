规则 暗门襟

输入 点 i
输入 点 j
输入 点 e
输入 参数 width

直线 ie = i 连接 e
参数 l_ie = ie.长度

点 v = 方向向量(j, i)
点 men1 = 求偏移(i, -width, v)
点 men5 = 求偏移(i, 0.5, v)
v = 方向向量(i, e)
点 men2 = 求偏移(men1, l_ie - 4, v)
v = (-1, 0)
点 men4 = 求偏移(e, 0.5, v)
点 men3 = 求门襟曲线辅助点(men2, e)


规则 ---- 门襟 ----

点 i2 = 求偏移(i, width + 3, v)
点 down = (0, 1)
点 e2 = 求偏移(i2, l_ie, down)
点 m1 = 求偏移(i2, -width, v)
点 m2 = 求偏移(m1, l_ie - 4, down)
点 m3 = 求门襟曲线辅助点(m2, e2)

路径 menjin = i2 连接 m1 连接 m2 圆顺 m3 圆顺 e2 连接 i2
menjin = menjin.标记(内部)

点 menjin1 = 等分点(i2, m1, 0.5)
点 down = (0, 1)
menjin1 = 求偏移(menjin1, 2, down)
路径 menjin_wl = 垂直经线(menjin1, l_ie-5)
menjin_wl = menjin_wl.命名(门襟)


规则 ---- 里襟 ----

参数 x0 = i2.横坐标
x0 = x0 - 1.5
点 i3 = 求水平镜像点(i2, x0)
点 e3 = 求水平镜像点(e2, x0)
点 me3 = 求水平镜像点(m3, x0)
点 me2 = 求水平镜像点(m2, x0)
点 me1 = 求水平镜像点(m1, x0)
点 c1 = 求偏移(i3, width + 3, v)
点 c3 = 求偏移(me2, 1.55, v)
c3 =  求偏移(c3, 0.5, down)
点 c2 = 等分点(c1, c3, 0.25)
c2 =  求偏移(c2, -0.2, v)
点 c5 = 求偏移(e3, 1.5, down)
点 c4 = 求偏移(me3, 0.6, v)
c4 =  求偏移(c4, 1.45, down)

路径 lijin = c1 连接 i3 连接 c5 圆顺 c4 圆顺 c3 圆顺 c2 圆顺 c1 以及 e3 圆顺 me3 圆顺 me2 连接 me1
lijin = lijin.标记(内部)

点 lijin1 = 等分点(i3, me1, 0.5)
lijin1 = 求偏移(lijin1, 2, down)
路径 lijin_wl = 垂直经线(lijin1, l_ie-5)
lijin_wl = lijin_wl.命名(底襟)


路径 path = menjin 以及 lijin 以及 menjin_wl 以及 lijin_wl
输出 path & men1 & men2 & men3 & men4 & men5